apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.crashlytics'

//apply plugin: 'jacoco-android'

android {
    compileSdkVersion 29

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'proguard-project.txt'
        exclude 'project.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
    }

    defaultConfig {
        applicationId "com.droidtechlab.filemanager"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 15
        versionName "0.9.2"
        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
        config
    }

    aaptOptions {
        cruncherEnabled = false
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            testProguardFile 'tests-proguard.cfg'
        }

        release {
            signingConfig signingConfigs.release

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'


            //Other parameters
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
        }
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions 'static'

    productFlavors {
        fdroid {
            dimension 'static'
            buildConfigField "boolean", "IS_VERSION_FDROID", "true"
        }

        play {
            dimension 'static'
            buildConfigField "boolean", "IS_VERSION_FDROID", "false"
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

ext {
    robolectricVersion = '3.8'
    glideVersion = '4.11.0'
    sshjVersion = '0.26.0'
    jcifsVersion = '2.1.3'
    fabSpeedDialVersion = '3.1.1'
    roomVersion = '2.2.5'
    bouncyCastleVersion = '1.66'
    awaitilityVersion = "3.1.6"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.readystatesoftware.systembartint:systembartint:1.0.3'

    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:25.12.0')

    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-perf'

    implementation 'com.google.android.gms:play-services-ads:19.5.0'


    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // rating library
    implementation "com.vorlonsoft:androidrate:1.2.1"

//    implementation 'com.github.hotchemi:android-rate:1.0.1'


    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation 'com.android.billingclient:billing:3.0.1'
    // RxJava support for Room
    implementation "androidx.room:room-rxjava2:$roomVersion"
    annotationProcessor 'androidx.annotation:annotation:1.1.0'


    //Detect memory leaks
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.3'

    implementation 'org.apache.commons:commons-compress:1.18'

    implementation('com.github.junrar:junrar:0.7') {
        exclude module: 'commons-logging'
        exclude module: 'commons-logging-api'
        exclude module: 'maven-scm-api'
        exclude module: 'maven-scm-provider-svnexe'
    }

    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

    //FTP Server

//    implementation 'org.apache.mina:mina-core:2.0.13'
//    implementation 'org.apache.ftpserver:ftpserver-core:1.0.6'
//    implementation 'org.apache.ftpserver:ftplet-api:1.0.6'


    // https://mvnrepository.com/artifact/org.apache.mina/mina-core

    implementation group: 'org.apache.mina', name: 'mina-core', version: '2.0.16'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    implementation group: 'org.slf4j', name: 'slf4j-android', version: '1.7.25'
    // https://mvnrepository.com/artifact/org.slf4j/jcl-over-slf4j
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.25'

    //implementation files('libs/ftplet-api-1.1.0-SNAPSHOT.jar')
    // https://mvnrepository.com/artifact/org.apache.ftpserver/ftplet-api
    implementation group: 'org.apache.ftpserver', name: 'ftplet-api', version: '1.1.0'

    //implementation files('libs/ftpserver-core-1.1.0-SNAPSHOT.jar')
    // https://mvnrepository.com/artifact/org.apache.ftpserver/ftpserver-core
    implementation group: 'org.apache.ftpserver', name: 'ftpserver-core', version: '1.1.0'

    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'com.android.volley:volley:1.1.1'

    implementation 'eu.chainfire:libsuperuser:1.0.0.+'

    implementation 'com.cloudrail:cloudrail-si-android:2.22.4'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'//Nice charts and graphs

    implementation 'com.github.npgall:concurrent-trees:2.6.1'//Concurrent tries

    //SFTP
    implementation "com.hierynomus:sshj:$sshjVersion"
    implementation "eu.agno3.jcifs:jcifs-ng:$jcifsVersion"

    implementation "org.bouncycastle:bcpkix-jdk15on:$bouncyCastleVersion"
    implementation "org.bouncycastle:bcprov-jdk15on:$bouncyCastleVersion"

    implementation "eu.agno3.jcifs:jcifs-ng:$jcifsVersion"

    //Glide: loads icons seemlessly
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation("com.github.bumptech.glide:recyclerview-integration:$glideVersion") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    implementation "com.leinardi.android:speed-dial:$fabSpeedDialVersion"


    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'


    implementation 'androidx.multidex:multidex:2.0.1'//Multiple dex files
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'

    //zip4j: support password-protected zips
    implementation 'net.lingala.zip4j:zip4j:1.3.2'

    implementation 'org.tukaani:xz:1.8'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 3.x.x version)
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.1.1'

    implementation project(':commons_compress_7z')
    implementation "androidx.core:core-ktx:+"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module("commons-logging:commons-logging-api:1.1") with module("commons-logging:commons-logging:1.1.1")
            substitute module("com.android.support:support-annotations:27.1.1") with module("com.android.support:support-annotations:27.0.2")
        }
    }
}

task supportOldLangCodes

[['id', 'in'], ['yi', 'ji'], ['he', 'iw']].forEach { sourceCode, destinationCode ->
    def copyTask = tasks.create('copyStrings' + sourceCode + 'Into' + destinationCode, Copy) {
        description('copying values-' + sourceCode + ' strings into values-' + destinationCode)
        from('src/main/res/values-' + sourceCode)
        into('src/main/res/values-' + destinationCode)
        include('strings.xml')
    }
    supportOldLangCodes.dependsOn copyTask
}

project.afterEvaluate {
    preBuild.dependsOn supportOldLangCodes
}

clean.dependsOn supportOldLangCodes
clean.mustRunAfter supportOldLangCodes

//jacoco {
//    toolVersion = "0.8.4"
//}
//
//tasks.withType(Test) {
//    jacoco.includeNoLocationClasses = true
//}

android.signingConfigs.release.storeFile = file("../release.jks")
android.signingConfigs.release.storePassword = 'developerx'
android.signingConfigs.release.keyAlias = 'key'
android.signingConfigs.release.keyPassword = 'developerx'
repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
